module

public import Mathlib.Combinatorics.Quiver.ReflQuiver
public import Mathlib.RepresentationTheory.Homological.GroupCohomology.Functoriality

@[expose] public section

namespace RepresentationTheory

universe u

variable {k G : Type u} [CommRing k] [Group G]

open groupCohomology CategoryTheory

/-- For `X : Rep k G`, `zeroι X` is the morphism `H0 X ⇨ X.V` in `ModuleCat k`. -/
noncomputable def groupCohomology.zeroι (X : Rep k G) : H0 X ⟶ ModuleCat.of k X.V :=
  (H0Iso X).hom ≫ ModuleCat.ofHom (Submodule.subtype X.ρ.invariants)

@[reassoc (attr := simp)]
lemma groupCohomology.zeroι_naturality {X Y : Rep k G} (f : X ⟶ Y) :
    groupCohomology.map (.id _) f 0 ≫ groupCohomology.zeroι Y = zeroι X ≫ f.toModuleCatHom := by
  aesop (add simp zeroι)

variable (k G) in
set_option backward.isDefEq.respectTransparency false in
/-- `zeroEmb` is the natural transformation from the `H0 : Rep k G ⥤ ModuleCat k` functor to
the forgetful functor `Rep k G ⥤ ModuleCat k`. -/
noncomputable def groupCohomology.zeroEmb : functor k G 0 ⟶ forget₂ (Rep k G) (ModuleCat k) where
  app X := groupCohomology.zeroι X
  naturality X Y f := by
    simpa only [functor_map, Rep.forget₂_moduleCat_map] using groupCohomology.zeroι_naturality f

end RepresentationTheory
